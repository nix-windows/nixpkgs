{ stdenv, fetchFromGitHub, python3, buildDocs ? true, asciidoc, docbook_xml_dtd_45, docbook_xsl, libxslt, re2c }:

with stdenv.lib;

let
  name = "ninja-${version}";
  version = "1.8.2";
  meta = {
    description = "Small build system with a focus on speed";
    longDescription = ''
      Ninja is a small build system with a focus on speed. It differs from
      other build systems in two major respects: it is designed to have its
      input files generated by a higher-level build system, and it is designed
      to run builds as fast as possible.
    '';
    homepage = https://ninja-build.org/;
    license = licenses.asl20;
    platforms = platforms.unix ++ platforms.windows;
    maintainers = with maintainers; [ thoughtpolice bjornfor orivej ];
  };
in

if stdenv.hostPlatform.isMicrosoft then # files have no executable flag

stdenv.mkDerivation rec {
  inherit name version meta;

  src = fetchFromGitHub {
    owner = "ninja-build";
    repo = "ninja";
    rev = "v${version}";
    sha256 = "0rjixgfrx3r5m11p500k2vw5isidn915xcbw1yk2594nhgnhmxn9"; # files have no executable flag, so sha256 is different
  };

  nativeBuildInputs = [ python3 ];

  buildPhase = ''
    system("python configure.py --bootstrap") == 0 or die;
  '';

  installPhase = ''
    mkdir($ENV{out}) or die "mkdir: $!";
    mkdir("$ENV{out}/bin") or die "mkdir: $!";
    copy("ninja.exe", "$ENV{out}/bin") or die "copy: $!";
  '';

# setupHook = ./setup-hook.pl;
}

else

stdenv.mkDerivation rec {
  inherit name version meta;

  src = fetchFromGitHub {
    owner = "ninja-build";
    repo = "ninja";
    rev = "v${version}";
    sha256 = "16scq9hcq6c5ap6sy8j4qi75qps1zvrf3p79j1vbrvnqzp928i5f";
  };

  nativeBuildInputs = [ python3 ] ++ optionals buildDocs [ asciidoc docbook_xml_dtd_45 docbook_xsl libxslt.bin re2c ];

  buildPhase = ''
    python configure.py --bootstrap
  '' + optionalString buildDocs ''
    # "./ninja -vn manual" output copied here to support cross compilation.
    asciidoc -b docbook -d book -o build/manual.xml doc/manual.asciidoc
    xsltproc --nonet doc/docbook.xsl build/manual.xml > doc/manual.html
  '';

  installPhase = ''
    install -Dm555 -t $out/bin ninja
    install -Dm444 misc/bash-completion $out/share/bash-completion/completions/ninja
    install -Dm444 misc/zsh-completion $out/share/zsh/site-functions/_ninja
  '' + optionalString buildDocs ''
    install -Dm444 -t $out/share/doc/ninja doc/manual.asciidoc doc/manual.html
  '';

  setupHook = ./setup-hook.sh;
}
